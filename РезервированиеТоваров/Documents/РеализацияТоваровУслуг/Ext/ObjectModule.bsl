
&После("ОбработкаПроведения")
Процедура РЗ_ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Отказ Тогда
		Возврат
	КонецЕсли;
	
	Движения.РЗ_ЗарезервированныеТовары.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	РЗ_ЗарезервированныеТоварыОстатки.Номенклатура КАК Номенклатура,
	|	РЗ_ЗарезервированныеТоварыОстатки.СчетПокупателю КАК СчетПокупателю,
	|	ВЫБОР
	|		КОГДА РЗ_ЗарезервированныеТоварыОстатки.КоличествоОстаток < РеализацияТоваровУслугТовары.Количество
	|			ТОГДА РЗ_ЗарезервированныеТоварыОстатки.КоличествоОстаток
	|		ИНАЧЕ РеализацияТоваровУслугТовары.Количество
	|	КОНЕЦ КАК Количество
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РЗ_ЗарезервированныеТовары.Остатки КАК РЗ_ЗарезервированныеТоварыОстатки
	|		ПО РеализацияТоваровУслугТовары.Номенклатура = РЗ_ЗарезервированныеТоварыОстатки.Номенклатура
	|			И РеализацияТоваровУслугТовары.Ссылка.СчетНаОплатуПокупателю = РЗ_ЗарезервированныеТоварыОстатки.СчетПокупателю
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
	|	И РЗ_ЗарезервированныеТоварыОстатки.КоличествоОстаток > 0";
	
	ТаблицаСписанияСРезервирования = Запрос.Выполнить().Выгрузить();
	
	Для каждого ТекСтрока Из ТаблицаСписанияСРезервирования Цикл
		Движение = Движения.РЗ_ЗарезервированныеТовары.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Количество = ТекСтрока.Количество;
		Движение.Номенклатура = ТекСтрока.Номенклатура;
		Движение.СчетПокупателю = ТекСтрока.СчетПокупателю;
	КонецЦикла;
	
КонецПроцедуры
