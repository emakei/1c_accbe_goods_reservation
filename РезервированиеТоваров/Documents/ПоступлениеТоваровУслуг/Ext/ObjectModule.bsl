
&После("ОбработкаПроведения")
Процедура РЗ_ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.РЗ_ПотребностьВТоварах.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", КонецДня(Дата));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ПоступлениеТоваровУслугТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_ДанныеПоступления
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугТовары.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РЗ_ПотребностьВТоварахОстатки.Номенклатура КАК Номенклатура,
	|	РЗ_ПотребностьВТоварахОстатки.СчетПокупателю КАК СчетПокупателю,
	|	ВЫБОР
	|		КОГДА РЗ_ПотребностьВТоварахОстатки.КоличествоОстаток < ВТ_ДанныеПоступления.Количество
	|			ТОГДА РЗ_ПотребностьВТоварахОстатки.КоличествоОстаток
	|		ИНАЧЕ ВТ_ДанныеПоступления.Количество
	|	КОНЕЦ КАК Количество
	|ИЗ
	|	ВТ_ДанныеПоступления КАК ВТ_ДанныеПоступления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РЗ_ПотребностьВТоварах.Остатки(&Период, ) КАК РЗ_ПотребностьВТоварахОстатки
	|		ПО ВТ_ДанныеПоступления.Номенклатура = РЗ_ПотребностьВТоварахОстатки.Номенклатура
	|ГДЕ
	|	ВЫБОР
	|			КОГДА РЗ_ПотребностьВТоварахОстатки.КоличествоОстаток < ВТ_ДанныеПоступления.Количество
	|				ТОГДА РЗ_ПотребностьВТоварахОстатки.КоличествоОстаток
	|			ИНАЧЕ ВТ_ДанныеПоступления.Количество
	|		КОНЕЦ > 0";
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	Для каждого ТекСтрока Из ТаблицаЗначений Цикл
		Движение = Движения.РЗ_ПотребностьВТоварах.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		ЗаполнитьЗначенияСвойств(Движение, ТекСтрока);
	КонецЦикла;
	
КонецПроцедуры
